apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.3.6'
group 'com.dinuscxj'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/circleprogressbar-release.aar")
            groupId 'com.dinuscxj'
            artifactId 'circleprogressbar'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project['bintrayUser'] : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project['bintrayApiKey'] : System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    publish = true
    dryRun = false
    pkg {
        repo = 'maven'
        name = 'circleprogressbar'

        description = "A circular android ProgressBar library which extends ProgressBar, It has solid,line and solid_line three styles. Besides, progress value can be freely customized."
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dinuscxj/CircleProgressBar.git'
        websiteUrl = 'https://github.com/dinuscxj/CircleProgressBar'
        issueTrackerUrl = 'https://github.com/dinuscxj/CircleProgressBar/issues'
        githubRepo = 'https://github.com/dinuscxj/CircleProgressBar'
        githubReleaseNotesFile = 'README.md'
        version {
            name = this.version
            desc = "support blur"
            released = new Date()
            vcsTag = this.version
        }
    }
}